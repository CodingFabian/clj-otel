= Guides
:toc:
:toclevels: 3
:icons: font

== Add telemetry to your library or an application

=== Add automatic instrumentation when running an application

* Follow the guide to <<_run_with_the_opentelemetry_auto_instrumentation_agent,run the application with the OpenTelemetry instrumentation agent>>.

=== Add manual instrumentation to your library or application code

* Add project dependency :
+
.`deps.edn`
[source,clojure]
----
{;; ...
 :deps {com.github.steffan-westcott/clj-otel-api {:mvn/version "0.1.0-SNAPSHOT"}}}
----

* Follow the guides in the remainder of this section to make changes to your library or application code to add instrumentation.

==== Create a synchronous span using the current context

* Use `steffan-westcott.otel.api.trace.span/with-span!` to wrap a body of forms in a span, where the current context is used to keep track of (potentially nested) spans:
+
[source,clojure]
----
(defn get-nums [args]
  (span/with-span! {:name "Getting numbers"}
    (fetch-nums args)))
----

==== Create a synchronous span using explicit context

* Use `steffan-westcott.otel.api.trace.span/with-span-binding` to wrap a body of forms in a span, where the context to use is passed in as the `:parent` option.
The new context containing the new span is bound to `context*` in this example:
+
[source,clojure]
----
(defn get-nums [context args]
  (span/with-span-binding [context* {:parent context
                                     :name "Getting numbers"}]
    (fetch-nums context* args)))
----

==== Create an asynchronous span

* Use `steffan-westcott.otel.api.trace.span/async-span` to start a new span that ends when either success/failure callback `respond`/`raise` is evaluated:
+
[source,clojure]
----
(defn get-nums-async [context args respond raise]
  (span/async-span {:parent context
                    :name   "Getting numbers"
                    :kind   :client}
                   (fn [context* respond* raise*]
                     (fetch-nums-async context* args respond* raise*))
                   respond
                   raise))
----

==== Add attributes to a span

* Use the `:attributes` option to add attributes when creating a span:
+
[source,clojure]
----
(defn user-info [user-id]
  (span/with-span! {:name "Getting user info"
                    :attributes {:user-id user-id}}
    (fetch-user-info user-id)))
----

* Alternatively, use `steffan-westcott.otel.api.trace.span/add-span-data!` to add attributes to an existing span.
+
By default, the span in the current context is updated:
+
[source,clojure]
----
(defn user-info [user-id]
  (span/add-span-data! {:attributes {:user-id user-id}})
  (fetch-user-info user-id))
----
+
Use the `:context` option to specify the context containing the span to update:
+
[source,clojure]
----
(defn user-info [context user-id]
  (span/add-span-data! {:context context
                        :attributes {:user-id user-id}})
  (fetch-user-info context user-id))
----

==== Add an event to a span

* Use `steffan-westcott.otel.api.trace.span/add-span-data!` to add an event to an existing span.
The event may include attributes.
+
By default, the event is added to the span in the current context:
+
[source,clojure]
----
(defn complete-stage [job-state]
  (span/add-span-data! {:event {:name "Stage completed"
                                :attributes (select-keys job-state [:stage :status])}})
  (notify-watchers job-state))
----
+
Use the `context:` option to specify the context containing the span to add the event to:
+
[source,clojure]
----
(defn complete-stage [context job-state]
  (span/add-span-data! {:event {:context context
                                :name "Stage completed"
                                :attributes (select-keys job-state [:stage :status])}})
  (notify-watchers context job-state))
----

==== Add an exception event to a span

NOTE: Events for exceptions thrown in spans which leave the span's scope are *automatically added* to the span.
This applies to synchronous and asynchronous spans.
For `async-span`, exceptions are "thrown" using the `raise` callback function.

* Use `steffan-westcott.otel.api.trace.span/add-exception!` to add an event describing an exception to an existing span.
The exception event may include attributes.
Use this function to capture details about caught (non-escaping) exceptions.
+
By default, the exception event is added to the span in the current context:
+
[source,clojure]
----
(defn process-args [args]
  (try
    (parse-args args)
    (catch Throwable e
      (span/add-exception! e {:escaping? false
                              :attributes {:args args}})
      {:result :parse-error})))
----
+
Use the `context:` option to specify the context containing the span to add the exception event to:
+
[source,clojure]
----
(defn process-args [context args]
  (try
    (parse-args args)
    (catch Throwable e
      (span/add-exception! e {:context context
                              :escaping? false
                              :attributes {:args args}})
      {:result :parse-error})))
----

=== Work with HTTP client and server spans

==== Use Ring middleware for server span support

* Use Ring middleware `steffan-westcott.otel.api.trace.http/wrap-server-span` to add HTTP server span support to a Ring handler.
+
The enabled support features vary with the selected middleware options.
The middleware can be configured to work in applications that run with or without the OpenTelemetry instrumentation agent.
It also supports both synchronous and asynchronous handlers.
+
An example using Jetty with server spans created by the agent:
+
[source,clojure]
----
(defn handler [request]
  ;; ...
  )

(def service
  (-> handler
      (trace-http/wrap-server-span {:create-span? false
                                    :server-name  "my-server"})))

(defonce server
  (jetty/run-jetty #'service {:port 8080 :join? false}))
----

==== Use Pedestal interceptors for server span support

* Use interceptors `steffan-westcott.otel.api.trace.http/server-span-interceptors` to add HTTP server span support to a Pedestal HTTP service.
+
The enabled support features vary with the selected interceptor options.
The interceptors can be configured to work in applications that run with or without the OpenTelemetry instrumentation agent.
+
An example using Jetty with server spans created by the agent:
+
[source,clojure]
----
(def routes
  (route/expand-routes
    [[["/" (trace-http/server-span-interceptors {:create-span? false
                                                 :server-name  "my-server"})
       ;; ...
       ]]]))

(def service-map
  {::http/routes routes
   ::http/type   :jetty
   ::http/port   8080
   ::http/join?  false})

(defonce server
  (http/start (http/create-server service-map)))
----

==== Add route data to a server span

* Use `steffan-westcott.otel.api.trace.http/add-route-data!` to add the matched route to a server span.
+
By default, the route data is added to the span in the current context:
+
[source,clojure]
----
(trace-http/add-route-data! "/rooms/:room-id")
----
+
Use the `context:` option to specify the context containing the span to add the route data to:
+
[source,clojure]
----
(trace-http/add-route-data! "/rooms/:room-id" {:context context})
----

==== Add HTTP response data to a client span

NOTE: When an application is run with the OpenTelemetry instrumentation agent, HTTP response data is *automatically added* to HTTP client spans.

* Use `steffan-westcott.otel.api.trace.http/add-client-span-response-data!` to add HTTP response data to a client span.
+
By default, the HTTP response data is added to the span in the current context:
+
[source,clojure]
----
(trace-http/add-client-span-response-data! response)
----
+
Use the `context:` option to specify the context containing the span to add the HTTP response data to:
+
[source,clojure]
----
(trace-http/add-client-span-response-data! response {:context context})
----

== Configure and run an application with telemetry

The options below determine what telemetry data is emitted from an application as it runs. Select one of these options and follow the linked guide:

* <<_run_with_the_opentelemetry_auto_instrumentation_agent,Run with the OpenTelemetry auto-instrumentation agent>> : Select this option to emit telemetry data as a result of automatic and manual instrumentation.
* <<_run_with_autoconfigured_sdk,Run with autoconfigured SDK>> : Select this option to emit telemetry data from manual instrumentation only; The SDK will be configured using properties or environment variables.
* <<_run_with_programmatically_configured_sdk,Run with programmatically configured SDK>> : Select this option to emit telemetry data from manual instrumentation only; The SDK will be configured programmatically in the application.
* <<_run_without_agent_or_sdk,Run without agent or SDK>> : Select this option to mute all telemetry data.

TIP: Traces telemetry data is muted in the last option, or by setting the property `otel.traces.exporter` to `none` (the default is `otlp`) when using either of the first two options (autoconfiguration). `otel.metrics.exporter` has default `none`.

[#_run_with_the_opentelemetry_auto_instrumentation_agent]
=== Run with the OpenTelemetry auto-instrumentation agent

* Download the https://github.com/open-telemetry/opentelemetry-java-instrumentation/releases/latest/download/opentelemetry-javaagent.jar[latest version of the OpenTelemetry instrumentation agent JAR].
The agent JAR includes the SDK and all its dependencies.
* Configure the agent and SDK, using properties and environment variables.
See the https://github.com/open-telemetry/opentelemetry-java-instrumentation/blob/main/docs/agent-config.md[agent and SDK configuration documentation].
* When running the application, enable the agent with the `-javaagent` JVM flag.

For an example application `my-app`, with `deps.edn` to export traces using OTLP over gRPC, use an alias like the following:

.`deps.edn`
[source,clojure]
[.small]
----
{;; ...
 :aliases {
   :otel {:jvm-opts ["-javaagent:path/to/opentelemetry-javaagent.jar"
                     "-Dotel.resource.attributes=service.name=my-app"
                     "-Dotel.traces.exporter=otlp"
                     "-Dotel.exporter.otlp.traces.protocol=grpc"]}}}
----

[#_run_with_autoconfigured_sdk]
=== Run with autoconfigured SDK

* Add project dependencies:
** _Required:_ `io.opentelemetry/opentelemetry-sdk-extension-autoconfigure` for the SDK itself and SDK autoconfiguration
** _Required:_ `io.opentelemetry/opentelemetry-exporter-???` for any required exporters
** _Optional:_ `io.opentelemetry/opentelemetry-sdk-extension-resources` for various resources to be automatically added to telemetry data
** _Optional:_ `io.grpc/grpc-netty-shaded`, `io.grpc/grpc-protobuf` and `io.grpc/grpc-stub` to use Netty for gRPC transport rather than the default OkHttp (see example below).
This is not needed if gRPC is not used by any exporters or the application.

* Configure the SDK using properties and environment variables.
See https://github.com/open-telemetry/opentelemetry-java/tree/main/sdk-extensions/autoconfigure[SDK autoconfigure configuration documentation].

For an example application `my-app`, with `deps.edn` to export traces using OTLP over gRPC with Netty transport, use an alias like the following:

.`deps.edn`
[source,clojure]
[.small]
----
{;; ...
 :aliases {
   :otel {:jvm-opts ["-Dotel.resource.attributes=service.name=my-app"
                     "-Dotel.traces.exporter=otlp"
                     "-Dotel.exporter.otlp.traces.protocol=grpc"]
          :extra-deps {io.opentelemetry/opentelemetry-sdk-extension-autoconfigure {:mvn/version "1.9.1-alpha"}
                       io.opentelemetry/opentelemetry-sdk-extension-resources     {:mvn/version "1.9.1"}
                       io.opentelemetry/opentelemetry-exporter-otlp-trace         {:mvn/version "1.9.1"}
                       io.grpc/grpc-netty-shaded                                  {:mvn/version "1.42.1"}
                       io.grpc/grpc-protobuf                                      {:mvn/version "1.42.1"}
                       io.grpc/grpc-stub                                          {:mvn/version "1.42.1"}}}}}
----

[#_run_with_programmatically_configured_sdk]
=== Run with programmatically configured SDK

* Add project dependencies:
** _Required:_ `com.github.steffan-westcott/clj-otel-sdk` for the SDK itself and a Clojure wrapper of SDK configuration
** _Required:_ `com.github.steffan-westcott/clj-otel-exporter-???` for Clojure wrapped versions of any required exporters
** _Optional:_ `com.github.steffan-westcott/clj-otel-sdk-extension-resources` for Clojure wrapped versions of various resources to add to telemetry data
** _Optional:_ `io.grpc/grpc-netty-shaded`, `io.grpc/grpc-protobuf` and `io.grpc/grpc-stub` to use Netty for gRPC transport rather than the default OkHttp (see example below).
This is not needed if gRPC is not used by any exporters or the application.


For an example application `my-app`, with `deps.edn` to export traces using OTLP over gRPC with Netty transport, use deps like the following:

.`deps.edn`
[source,clojure]
[.small]
----
{;; ...
 :deps {com.github.steffan-westcott/clj-otel-sdk                      {:mvn/version "0.1.0-SNAPSHOT"}
        com.github.steffan-westcott/clj-otel-sdk-extension-resources  {:mvn/version "0.1.0-SNAPSHOT"}
        com.github.steffan-westcott/clj-otel-exporter-otlp-grpc-trace {:mvn/version "0.1.0-SNAPSHOT"}
        io.grpc/grpc-netty-shaded                                     {:mvn/version "1.42.1"}
        io.grpc/grpc-protobuf                                         {:mvn/version "1.42.1"}
        io.grpc/grpc-stub                                             {:mvn/version "1.42.1"}}}
----

[#_run_without_agent_or_sdk]
=== Run without agent or SDK

There are no steps to add dependencies or otherwise configure the application in order to run it without the agent or SDK.

NOTE: An application run without the OpenTelemetry instrumentation agent or SDK will not emit any telemetry data. Usage of the OpenTelemetry API (manual instrumentation) in the application will invoke no-op implementations.

== Configure span export

=== Batch spans prior to export

=== Export spans to the OpenTelemetry Collector

=== Export spans directly to backends

==== Export spans directly to backends that support OTLP

==== Export spans directly to Jaeger

==== Export spans directly to Zipkin

== Use the OpenTelemetry Collector

=== Deploy the OpenTelemetry Collector

=== Configure the OpenTelemetry Collector